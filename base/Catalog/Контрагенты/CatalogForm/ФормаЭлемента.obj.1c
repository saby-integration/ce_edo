// Устанавливает значения индикаторов надежности. Вызвается после проверки авторизации СБИС в обработке оповещения.
//
// Параметры:
//  Результат  - Неопределено, Булево - авторизация прошла успешно или 
//                 не удалось проверить авторизацию
//  ДополнительныеПараметры  - Произвольный - дополнительные параметры, возвращаемые обработчиком оповещения.
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура УстановитьЗначенияИндикаторов(Результат, ДополнительныеПараметры) Экспорт

	Если Не Открыта() Тогда
		Возврат;
	КонецЕсли;
	
	Если saby_ВызовСервераПереопределяемый.ДоступныРолиСбис() И Объект.ИНН <> "" Тогда
		saby_Клиент.УстановитьЗначенияИндикаторов(ЭтаФорма, ?(Не Модифицированность И ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, Объект.ИНН), Результат <> Неопределено И Результат);
	КонецЕсли;

КонецПроцедуры // УстановитьЗначенияИндикаторов(Результат, ДополнительныеПараметры)

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура saby_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если saby_ВызовСервераПереопределяемый.ДоступныРолиСбис() Тогда 
		saby_ВызовСервера.СоздатьЭлементыИндикатораНадежности(ЭтаФорма, "ГруппаПраваяКолонка", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура saby_ПриОткрытииПосле(Отказ)
	
	УстановитьЗначенияИндикаторов(Истина, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобыйтийЭлементовФормы

&НаКлиенте
Процедура saby_ДекорацияПоказатьНажатие(Элемент)

		saby_Клиент.ПроверитьАвторизоваться(Новый ОписаниеОповещения("УстановитьЗначенияИндикаторов", ЭтотОбъект));

КонецПроцедуры
	
&НаКлиенте
Процедура saby_ИндикаторСБИСНажатие(Элемент, СтандартнаяОбработка)

	saby_Клиент.ИндикаторСБИСНажатие(ЭтаФорма, Объект.Ссылка, Объект.ИНН, СтандартнаяОбработка);
	
КонецПроцедуры
	
#КонецОбласти

#Область ПереопределяемыЗаимствованияМетодовФормы
	
&НаКлиенте
&После("ЗапуститьПроверкуКонтрагента")
Процедура saby_ЗапуститьПроверкуКонтрагента()
	
	УстановитьЗначенияИндикаторов(Истина, Неопределено)
	
КонецПроцедуры

&НаСервере
&После("УстановитьВидимостьЭлементов")
Процедура saby_УстановитьВидимостьЭлементов()
	
	//Отключим видимость кнопки заполнения по наименованию, т.к. для этого требуется подключить 1С.Контрагент, который мы перекрываем
	Элементы.КнопкаЗаполнитьРеквизитыПоНаименованию.Видимость = Ложь;
	
	Если Не saby_ВызовСервераПереопределяемый.ДоступныРолиСбис() Тогда 
		Возврат;
	КонецЕсли;
	
	Попытка
		Элементы.ГруппаСБИСНадежность.Видимость = Объект.СтранаРегистрации = Справочники.СтраныМира.Россия;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
&После("ЗаполнитьРеквизитыПоДаннымЕГРНаКлиенте")
Процедура saby_ЗаполнитьРеквизитыПоДаннымЕГРНаКлиенте()
	
	УстановитьЗначенияИндикаторов(Истина, Неопределено)
	
КонецПроцедуры

#КонецОбласти

